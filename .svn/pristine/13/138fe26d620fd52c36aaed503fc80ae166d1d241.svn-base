unit MessageEdit;

Message o_message;
Pattern activity; //this is used only for when user presses save and nav button
string messageID;

PatternMessageText messageOrder;

bool isEdit;

string msg;
string nav_return;

void init() {

	if (o_message == null){
		o_message = Message:new();

		if (activity != null){
			messageID = activity.patternID;	
		}
		
		isEdit = false;
	} else {
		isEdit = true;
		messageID = o_message._MTID;
	}

	if (nav_return == null){
		nav_return = "nav_message_management";
	}
	
}

string back(){
	resetVars();

	if (nav_return == "nav_message_order_management" && activity != null){
		MessageOrderManagement:activityFilter = activity;
	}

	return nav_return;
}

void resetVars(){
	activity = null;
	o_message = null;
	messageOrder = null;
	messageID = null;
}

bool saveMessage() {

	if(o_message == null){ 
		msg = "Message is null";
		Alerter:alertError(msg);
		return false;
	}

	if(messageID == null || String:length(messageID) <= 0){ 
		msg = "Please enter Message ID";
		Alerter:alertError(msg);
		return false;
	}

	if(o_message._MTID_order == null) { 
		o_message._MTID_order = 0; 
	}

	if(o_message._MT_text == null) { 
		o_message._MT_text = "";
	}

	if(o_message.maxMultipleChoice == null) {
		o_message.maxMultipleChoice = 0;
	}

	if(o_message.correctAnswer == null){ 
		o_message.correctAnswer = "";
	}

	if(o_message.informCorrectText == null){
		o_message.informCorrectText = "";
	}

	Pattern[] activites = ActivityController:getActivityFromActivityID(messageID);

	if (activites.length() > 0){
		msg = "Message ID is the same as a Activity ID, Please ensure that the Message ID is unique";
		Alerter:alertError(msg);
		return false;
	}

	Message m = MessageController:getMessageForMessageID(messageID);
	if(m != null){ 
		if (isEdit == true){
			if (o_message._MTID != messageID){
				msg = String:concat("Message ID ", messageID ," already exists please enter a unique ID, Message NOT updated");
   				Alerter:alertError(msg);
				return false;
			}
		} else {
			msg = String:concat("Message ID ", messageID ," already exists please enter a unique ID, Message NOT saved");
			Alerter:alertError(msg);
			return false;
		}
	}

	o_message._MTID = messageID;
	o_message.save();

	msg = String:concat("Message ID ", messageID, " saved");
	Alerter:alert(msg);

	return true;
}

string saveMessageButton(){
	
	if (saveMessage() == false){
		return null;
	}
	
	return back();
}

string saveMessageNavMessageOrderEditButton(){
	
	if (saveMessage() == false){
		return null;
	}

	MessageOrderEdit:o_message = o_message;
	MessageOrderEdit:activity = activity;
	MessageOrderEdit:messageOrder = messageOrder;
	nav_return = "nav_create_message_order";

	return back();
}