unit SequenceOrderEdit;

SequenceOrder sequenceOrder;

Topic subTopic;
Sequence sequence;

int orderNumber;

string nav_return;
string msg;

bool isEdit;

void init() {

	msg = "";

	if (sequenceOrder == null){
		sequenceOrder = SequenceOrder:new();
		isEdit = false;
		orderNumber = 0;
	} else {
		subTopic = sequenceOrder.topic_sequenceOrder;
		sequence = sequenceOrder.sequence_sequenceOrder;
		orderNumber = sequenceOrder.sequenceOrder;
		isEdit = true;
	}

	if (nav_return == null){
		nav_return = "nav_sequence_order_management";
	}

}

string saveSequenceOrder() {

	if (subTopic == null){
		msg = "Please select an Sub-Topic";
   		Alerter:alertError(msg);
   		return null;
	}	

	if (sequence == null){
		msg = "Please select a Sequence";
   		Alerter:alertError(msg);
   		return null;
	}

	if (orderNumber == null || orderNumber <= 0){
		msg = "Please enter a positive integer as a SequenceOrder number";
   		Alerter:alertError(msg);
   		return null;
	}

	if (SequenceOrderController:isSequenceOrderAssignedToSubTopic(subTopic, orderNumber) == true){

		if (isEdit == true){
			if (orderNumber != sequenceOrder.sequenceOrder){
				msg = String:concat("The SequenceOrder number ", orderNumber, " already exists for SequenceID", sequence.sequenceID, ", please enter a unique order number");
   				Alerter:alertError(msg);
   				return null;
			}
		} else {
			msg = String:concat("SubTopic ", subTopic.topicID, " already is associated to OrderNumber ", orderNumber, " please enter a unique order number");
   			Alerter:alertError(msg);
   			return null;
		}
	}

	if (SequenceOrderController:getSequenceOrderForSequenceSubTopic(sequence, subTopic) != 0 && isEdit == false){
		msg = String:concat("The Sequence and Sub-Topic are already connected with Sequence Order Number: ", orderNumber);
		Alerter:alertError(msg);
		return null;
	}

	sequenceOrder.sequenceOrder = orderNumber;
	sequenceOrder.topic_sequenceOrder = subTopic;
	sequenceOrder.sequence_sequenceOrder = sequence;
	sequenceOrder.save();
	
	msg = "Sequence Order Saved";
	Alerter:alert(msg);

	return navigate();
}

string navigate(){
	if (nav_return == "nav_sequence_order_management"){
		SequenceOrderManagement:sequenceFilter = null;
		SequenceOrderManagement:subTopicFilter = subTopic;
	}
	resetVars();
	return nav_return;
}

void resetVars(){
	subTopic = null;
	sequence = null;
	sequenceOrder = null;
}